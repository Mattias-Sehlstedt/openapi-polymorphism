plugins {
    id 'java'
    id "org.openapi.generator" version '7.19.0'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

apply plugin: 'io.spring.dependency-management'

version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    // Spring Boot Starter Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-json'

    // Annotations...
    implementation 'javax.annotation:javax.annotation-api:1.3'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.6'
    testImplementation "org.wiremock:wiremock-jetty12:3.13.1"
}

test {
    useJUnitPlatform()
}

var openApiGeneratedDir = "$rootDir/build/generated".toString()
var openApiGeneratedDirSrc = openApiGeneratedDir.concat("/src/main/java")

tasks.compileJava {
    dependsOn(tasks.openApiGenerate)
}

tasks.clean {
    delete("./generated")
}

sourceSets.main {
    java.srcDir(openApiGeneratedDirSrc)
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/artifact/artifact.json".replace("\\", "/"))
    outputDir.set(openApiGeneratedDir.toString())
    apiPackage.set("polymorphism.api")
    invokerPackage.set("polymorphism.invoker")
    modelPackage.set("polymorphism.model")
    configOptions.set([
            dateLibrary                   : "java8",
            openApiNullable               : "false",
            useJakartaEe                  : "true",
            useOneOfInterfaces            : "true",
            useSingleRequestParameter     : "static",
            additionalModelTypeAnnotations: "@lombok.experimental.SuperBuilder;@lombok.Data"
    ])
    templateDir.set("$rootDir/artifact/templates/webclient")
    modelNameSuffix = "DTO"
    generateApiDocumentation.set(false)
    generateApiTests.set(false)
    generateModelDocumentation.set(false)
    generateModelTests.set(false)
    library.set("webclient")
    ignoreFileOverride.set("$rootDir/artifact/.artifact.ignore")
    validateSpec.set(false)
}